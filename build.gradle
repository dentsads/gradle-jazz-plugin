apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group='com.dentsads.rtc.build.gradle'
version='1.0-SNAPSHOT'

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.google.guava:guava:18.0'
    compile "org.codehaus.groovy:groovy-backports-compat23:2.3.5"
    compile fileTree(dir: 'lib', includes: ['*.jar'])
}

def repositoryUri = uri('build/repo')

repositories {
    mavenCentral()
    maven { url repositoryUri}
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: mavenLocal().url)
        }
    }
}

publishing {
    repositories {
        maven {
            url repositoryUri
        }
    }
}

def prepareRTCUploads = { relativeLibDir ->
    fileTree(dir: relativeLibDir, includes: ['*.jar']).each {
        publishing.publications.create(it.getName(), MavenPublication.class)
        publishing.publications.getByName(it.getName()) {
            groupId 'com.ibm.team'
            artifactId it.getName().replaceFirst("[.][^.]+\$", "")
            version '5.0.2'

            artifact file(relativeLibDir + "/" + it.getName())
        }
    }
}

/* uncomment next line in order to add publishing containers for RTC Plain APIs.
 * After that execute the 'publish' or 'publishToMavenLocal' task in order to publish the APIs
 * to the specified repositoryUri or the local .m2 repository, respectively.
 */
//prepareRTCUploads('lib')
